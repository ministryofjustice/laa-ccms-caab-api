apply plugin: 'uk.gov.laa.springboot.laa-spring-boot-gradle-plugin'

configurations {
    mockitoAgent {
        transitive = false
    }
}

dependencies {

//    constraints {
//        testImplementation('org.apache.commons:commons-compress:1.28.0') {
//            because '1.24.0 has a known security vulnerability (CVE-2024-25710)' +
//                    "Parent: org.testcontainers:testcontainers"
//        }
//    }

    implementation project(':caab-api')

    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'io.micrometer:micrometer-tracing-bridge-brave'

    runtimeOnly 'com.oracle.database.jdbc:ojdbc11'

    //Enable access token authentication
    implementation 'uk.gov.laa.springboot:laa-spring-boot-starter-auth'

    //Metrics
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'io.micrometer:micrometer-registry-prometheus'
    implementation 'io.prometheus:prometheus-metrics-tracer-initializer'

    //Enable Swagger UI
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.8.13'

    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    implementation 'org.mapstruct:mapstruct:1.6.3'
    annotationProcessor 'org.mapstruct:mapstruct-processor:1.6.3'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.boot:spring-boot-restclient'
    testImplementation 'org.springframework.boot:spring-boot-resttestclient'
    testImplementation 'org.springframework.boot:spring-boot-testcontainers'

    //Used for integration tests
    testImplementation 'org.testcontainers:testcontainers'
    testImplementation 'org.testcontainers:testcontainers-junit-jupiter'
    testImplementation 'org.testcontainers:testcontainers-oracle-free'

    mockitoAgent "org.mockito:mockito-core"
}

tasks.withType(Test).configureEach {
    jvmArgs(
            "-javaagent:${configurations.mockitoAgent.asPath}",
            "-Xshare:off"
    )
}

test {
    useJUnitPlatform()

    finalizedBy jacocoTestReport
}

jacocoTestReport {
    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, exclude: [
                    '**/CaabApiApplication.class',
                    '**/ApplicationConfig.class',
                    '**/AuditorAwareImpl.class'])
        }))
    }
}

jacocoTestCoverageVerification {
    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, exclude: [
                    '**/CaabApiApplication.class',
                    '**/ApplicationConfig.class',
                    '**/AuditorAwareImpl.class'])
        }))
    }
}